plugins {
    id "fabric-loom" version "0.4-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url "https://dl.bintray.com/ladysnake/libs" }
    maven { url "https://mod-buildcraft.com/maven" }
    maven { url "https://dl.bintray.com/shedaniel/legacy-yarn-updated" }
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("me.shedaniel:legacy-yarn:${project.yarn_mappings}:v2")
    modCompile("net.fabricmc:fabric-loader:${project.loader_version}")
    modCompile("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems:${project.roughlyenoughitems}")
    modRuntime("me.shedaniel:RoughlyEnoughItems:${project.roughlyenoughitems}")
    depend("org.jetbrains:annotations:19.0.0")
    depend("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_components}")
    depend("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_components}")
    depend("alexiil.mc.lib:libblockattributes-core:${project.lba}") 
    depend("alexiil.mc.lib:libblockattributes-fluids:${project.lba}") 
    depend("me.shedaniel.cloth.api:cloth-api:${project.cloth}") 
}

def depend(String string) {
    dependencies.modApi(string) {
        exclude(module: "fabric")
    }
    dependencies.include(string)
}

minecraft {
    accessWidener(file("src/main/resources/hqm.accesswidener"))
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}
